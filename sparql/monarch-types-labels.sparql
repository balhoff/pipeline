PREFIX OBAN: <http://purl.org/oban/>
PREFIX OBO: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

CONSTRUCT {
    ?gene rdfs:label ?gene_label .
    ?gene OBO:RO_0002162 ?taxon .
    ?gene oboInOwl:hasExactSynonym ?synonym .
    ?gene rdfs:subClassOf ?type .
}
WHERE {
    ?gene rdfs:label ?gene_label .
    ?gene OBO:RO_0002162 ?taxon .
    ?gene rdfs:subClassOf ?type .
    OPTIONAL {
      ?gene dc:description|oboInOwl:hasExactSynonym ?synonym
    }
    FILTER(?type != owl:Class)
    FILTER(?type != owl:NamedIndividual)
}
