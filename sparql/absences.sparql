PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ps: <http://purl.org/phenoscape/vocab.owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

# Assumes reflexive rdfs:subClassOf closure
SELECT DISTINCT ?taxon (ps:has_absence_of AS ?p) ?entity
WHERE {
?taxon ps:exhibits_state/ps:describes_phenotype/rdfs:subClassOf/ps:absence_of ?phenotypeEntity .
?phenotypeEntity ps:implies_presence_of|^rdfs:subClassOf ?entity .
?entity rdfs:isDefinedBy <http://purl.obolibrary.org/obo/uberon.owl> .
?taxon rdfs:isDefinedBy <http://purl.obolibrary.org/obo/vto.owl> .
}
